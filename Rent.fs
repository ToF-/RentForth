\ Rent.fs
INCLUDE ffl/hnt.fs
INCLUDE ffl/hct.fs

20000 CONSTANT MAX-VALUES
VARIABLE VALUE-TIME-TABLE
VARIABLE MAX-VALUE
VARIABLE CURRENT-VALUE
0 VALUE-TIME-TABLE !

: DEBUG VALUE-TIME-TABLE @ HCT-DUMP CR .S KEY DROP ;

: UPDATE-MAX ( n adr -- )
    DUP @ ROT MAX SWAP ! ;

: S>KEY ( n -- adr,cnt )
    BASE @ SWAP 0 64 BASE ! <# #S #> ROT BASE ! ;

: UPDATE-VALUE ( n,k -- )
    S>KEY 2DUP 2>R VALUE-TIME-TABLE @ HCT-GET ( n,v,f )
    IF MAX THEN DUP 2R> VALUE-TIME-TABLE @ HCT-INSERT 
    MAX-VALUE UPDATE-MAX ;

: GET-VALUE ( k -- n,f | false )
    S>KEY VALUE-TIME-TABLE @ HCT-GET ;

: INIT-VALUES ( -- )
    VALUE-TIME-TABLE @ ?DUP IF HCT-FREE THEN
    MAX-VALUES HCT-NEW VALUE-TIME-TABLE !
    0 MAX-VALUE !
    0 CURRENT-VALUE ! ;


: UPDATE-VALUE-AT-START-TIME ( start -- )
    CURRENT-VALUE @ OVER UPDATE-VALUE 
    GET-VALUE DROP CURRENT-VALUE UPDATE-MAX ;
 

: ADD-ORDER ( start,duration,bid -- )
    -ROT OVER UPDATE-VALUE-AT-START-TIME 
    + SWAP CURRENT-VALUE @ + SWAP UPDATE-VALUE ;

