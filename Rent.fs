\ Rent.fs
INCLUDE ffl/hnt.fs
INCLUDE ffl/hct.fs

10000 CONSTANT MAX-ORDERS
VARIABLE HCT-VALUE 0 HCT-VALUE !
VARIABLE MAX-VALUE
VARIABLE CUR-VALUE

: UPDATE-MAX ( n adr -- )
    DUP @ ROT MAX SWAP ! ;

: S>KEY ( n -- adr,cnt )
    BASE @ SWAP 64 BASE !
    S>D <# #S #> 
    ROT BASE ! ;

: GET-HCT-VALUE ( k -- n,f | false )
    S>KEY HCT-VALUE @ HCT-GET ;

: SET-HCT-VALUE ( n k -- )
    S>KEY HCT-VALUE @ HCT-INSERT ;

: UPDATE-VALUE ( n,k -- )
    SWAP OVER GET-HCT-VALUE 
    IF MAX THEN
    DUP ROT SET-HCT-VALUE
    MAX-VALUE UPDATE-MAX ;
    
: FREE-HCT-VALUES
    HCT-VALUE @ ?DUP IF HCT-FREE THEN ;

: NEW-HCT-VALUES
    MAX-ORDERS 2* HCT-NEW HCT-VALUE ! ;

: INITIALIZE ( -- )
    FREE-HCT-VALUES
    NEW-HCT-VALUES
    0 MAX-VALUE ! 0 CUR-VALUE ! ;


: UPDATE-VALUE-AT-START-TIME ( start -- )
    CUR-VALUE @ OVER UPDATE-VALUE 
    GET-HCT-VALUE DROP CUR-VALUE UPDATE-MAX ;
 

: ADD-ORDER ( start,duration,bid -- )
    -ROT OVER UPDATE-VALUE-AT-START-TIME 
    + SWAP CUR-VALUE @ + SWAP UPDATE-VALUE ;

