\ Rent.fs
INCLUDE ffl/hct.fs

10000 CONSTANT MAX-ORDERS
MAX-ORDERS 2* CONSTANT RENT-HCT-SIZE
VARIABLE CURRENT-VALUE
VARIABLE RENT-VALUE
VARIABLE RENT-HCT-VALUES

: RENT-VALUES
    RENT-HCT-VALUES @ ;

: INIT-RENT
    0 RENT-VALUE ! 
    0 CURRENT-VALUE !
    RENT-VALUES ?DUP IF HCT-FREE THEN
    RENT-HCT-SIZE HCT-NEW RENT-HCT-VALUES ! ;

: S>KEY ( n -- addr c )
    S>D <# #S #> ;

: MAX! ( n addr -- )
    DUP @ ROT MAX SWAP ! ;

: END ( s d -- e )
    + ;

: RENT-VALUE#@ ( t -- n )
    S>KEY RENT-VALUES HCT-GET
    0= IF 0 THEN ;

: RENT-VALUE#! ( n t -- )
    S>KEY RENT-VALUES HCT-INSERT ;

: RENT ( s d b -- )
    CURRENT-VALUE @ + 
    DUP RENT-VALUE MAX! 
    -ROT END RENT-VALUE#! ;

: CASH ( t -- )
    RENT-VALUE#@ CURRENT-VALUE MAX! ;

1 CONSTANT RENT-CODE
0 CONSTANT CASH-CODE 
21 CONSTANT T-SIZE
1 T-SIZE LSHIFT 1- CONSTANT T-MASK

: ENCODE-RENT ( s d b -- evR )
    ROT 1 LSHIFT RENT-CODE OR
    T-SIZE LSHIFT ROT OR
    T-SIZE LSHIFT SWAP OR ;

: ENCODE-CASH ( s d -- evC )
    + 1 LSHIFT 42 LSHIFT ;

: ORDER>EVENTS ( s d b -- evR evC )
    >R 2DUP R> 
    ENCODE-RENT
    -ROT
    ENCODE-CASH ;

: RENT? ( ev -- t|f )
    1 42 LSHIFT AND ;

: DECODE-RENT-EVENT ( ev -- s d b xRent )
    DUP T-MASK AND >R T-SIZE RSHIFT
    DUP T-MASK AND >R T-SIZE RSHIFT
    1 RSHIFT R> R> ['] RENT ;

: DECODE-CASH-EVENT ( ev -- s xCash )
    T-SIZE RSHIFT T-SIZE RSHIFT 1 RSHIFT ['] CASH ;

: DECODE-EVENT ( ev -- s d b xRent | s xCash )
    DUP RENT? IF DECODE-RENT-EVENT ELSE DECODE-CASH-EVENT THEN ;
