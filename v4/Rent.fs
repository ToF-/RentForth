\ Rent.fs
INCLUDE ffl/hct.fs
INCLUDE ffl/act.fs

10000 CONSTANT MAX-ORDERS
MAX-ORDERS 2* CONSTANT RENT-HCTIME%
VARIABLE CURRENT-VALUE
VARIABLE RENT-VALUE
VARIABLE RENT-HCT-VALUES
VARIABLE RENT-ACT-EVENTS
1 CONSTANT RENT-CODE
0 CONSTANT CASH-CODE 
21 CONSTANT TIME%
1 TIME% LSHIFT 1- CONSTANT T-MASK

: RENT-VALUES RENT-HCT-VALUES @ ;

: RENT-EVENTS RENT-ACT-EVENTS @ ;

: INIT-RENT
    0 RENT-VALUE !  0 CURRENT-VALUE !
    RENT-EVENTS ?DUP IF ACT-FREE THEN ACT-NEW RENT-ACT-EVENTS ! 
    RENT-VALUES ?DUP IF HCT-FREE THEN RENT-HCTIME% HCT-NEW RENT-HCT-VALUES ! ;

: S>KEY ( n -- addr c ) S>D <# #S #> ;

: MAX! ( n addr -- ) DUP @ ROT MAX SWAP ! ;

: ORDER-END ( s d -- e ) + ;

: RENT-VALUE#@ ( t -- n )
    S>KEY RENT-VALUES HCT-GET 0= IF 0 THEN ;

: RENT-VALUE#! ( n t -- )
    S>KEY RENT-VALUES HCT-INSERT ;

: RENT ( s d b -- )
    CURRENT-VALUE @ + 
    DUP RENT-VALUE MAX! 
    -ROT ORDER-END RENT-VALUE#! ;

: CASH ( t -- )
    RENT-VALUE#@ CURRENT-VALUE MAX! ;

: ENCODE-RENT ( s d b -- evR )
    ROT 1 LSHIFT RENT-CODE OR
    TIME% LSHIFT ROT OR
    TIME% LSHIFT SWAP OR ;

: ENCODE-CASH ( s d -- evC )
    ORDER-END 1 LSHIFT TIME% 2* LSHIFT ;

: ORDER>EVENTS ( s d b -- evR evC )
    >R 2DUP R> ENCODE-RENT
    -ROT ENCODE-CASH ;

: RENT? ( ev -- t|f )
    1 TIME% 2* LSHIFT AND ;

: DECODE-RENT-EVENT ( ev -- s d b xRent )
    DUP T-MASK AND >R TIME% RSHIFT
    DUP T-MASK AND >R TIME% RSHIFT
    1 RSHIFT R> R> ['] RENT ;

: DECODE-CASH-EVENT ( ev -- s xCash )
    TIME% RSHIFT TIME% RSHIFT 1 RSHIFT ['] CASH ;

: DECODE-EVENT ( ev -- s d b xRent | s xCash )
    DUP RENT? IF DECODE-RENT-EVENT ELSE DECODE-CASH-EVENT THEN ;

: ADD-EVENT ( ev -- )
    DUP RENT-EVENTS ACT-INSERT ;

: ADD-ORDER ( s d b -- )
    ORDER>EVENTS ADD-EVENT ADD-EVENT ;

: EXECUTE-EVENT ( x x -- )
    DROP DECODE-EVENT EXECUTE ;

: COMPUTE-VALUE ( -- n )
    ['] EXECUTE-EVENT RENT-EVENTS ACT-EXECUTE
    RENT-VALUE @ ;

: .RENT ( s d b -- ) ROT . SWAP . . ." RENT" ;

: .CASH ( s -- ) . ." CASH" ;

: .EVENT ( x x -- )
    DECODE-EVENT CR ['] RENT = IF .RENT ELSE .CASH THEN
    DROP ;

: .EVENTS 
    ['] .EVENT RENT-EVENTS ACT-EXECUTE ;

    
